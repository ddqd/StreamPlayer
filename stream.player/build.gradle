apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 2
        versionName "1.1"
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
}


/**
 * Allows to package compiled classes into one jar
 */
android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.description 'Allows to package the library sources into the jar'
    task.baseName "StreamPlayer-${project.android.defaultConfig.versionName}"
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:26.1.0'
    compile 'com.netflix.rxjava:rxjava-core:0.20.4'
    compile 'com.netflix.rxjava:rxjava-android:0.20.4'

}
